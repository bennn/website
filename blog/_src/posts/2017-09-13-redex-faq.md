    Title: PLT Redex FAQ
    Date: 2017-09-13T01:26:46
    Tags: tutorial, PLT Redex, by Ben Greenman

A short guide to Redex concepts, conventions, and common mistakes.


<!-- more -->

<!-- TODO careful about "set" "syntactic category" etc. -->
<!-- TODO careful capitalization, technical terms -->
<!-- TODO split into "basic" and "FAQ" sections -->

### What is Redex useful for?

1. declaring [regular tree grammars](https://en.wikipedia.org/wiki/Regular_tree_grammar)
2. defining _pattern_-based judgments and relations on _terms_
3. testing properties of the above

More generally, Redex is helpful for experimenting with a programming language
 design, and helping you decide what you might want to prove about a language.


### What is Redex **not** useful for?

Proving theorems about a grammar, judgment, or relation.


### What is a _term_?

Informally, a term is:

- a Redex "atom", or
- an object that represents a sequence of characters.

More formally, a term is the result of evaluating `(term X)`, where `X` is
 any Racket expression.

Examples:

```
$ racket
Welcome to Racket v6.10.0.3.
> (require redex/reduction-semantics)
> (term 42)
42
> (term (+ 2 2))
'(+ 2 2)
> (term ("hello" world (#false)))
'("hello" world (#f))
```

Some terms may look strange.
That's OK, because a term by itself has no meaning.


### What is a _Redex model_?

A Redex model is collection of tools for working with terms.
The tools may include:

- _languages_, to define a grammar for terms
- _judgments_, to describe properties of terms or relations between terms
- _metafunctions_, to transform terms
- _reduction relations_, to defining a term rewriting system

The goal of these tools is to encode a "real thing" (maybe, a programming language)
 using Redex terms.


### What is a language?

A Redex _language_ is a named set of non-terminals, _patterns_, and _binding forms_.
For example, a Redex model of the natural numbers might start with this language:

```
(define-language nat
  [N ::= Zero
         (Plus1 N)])
```
- the name of the language is `nat`
- the non-terminal `N` is associated with two patterns: `Zero` and `(Plus1 N)`
- there are no _binding forms_

Each pattern describes a syntactic category of terms.
Each non-terminal gives a name to the union of patterns that follow it.

The non-terminal `N` describes all terms that are either:

1. the symbol `Zero`
2. lists of the form `(Plus1 N)`, where `N` is either `Zero` or another "Plus1"

For example,

```
(term Zero)
(term (Plus1 Zero))
(term (Plus1 (Plus1 Zero)))
(term (Plus1 (Plus1 (Plus1 Zero))))
;; .... and so on
```

If a language has binding forms, then some terms can introduce names.
See the FAQ entry on _binding forms_ for an example.


### What is a pattern?

A pattern is a sequence of characters and variables.
If you have: (1) a language `L`, and (2) a pattern that contains _named non-terminals_ from `L`,
 then you can ask whether a Redex term matches the pattern.

If the term `t` matches the pattern, then Redex can show how the _named non-terminals_
 in the pattern match sub-terms of `t`.

A _named non-terminal_ for a language `L` is an identifier made of:
 (1) a non-terminal from `L`, (2) an underscore (`_`), and (3) any other identifier.
See the FAQ entry below.

For example, `(redex-match? L p t)` returns `#true` if the term `t` matches
 the pattern `p` relative to the language `L`.

```
(define-language nat
  [N ::= Zero (Plus1 N)])

(redex-match? nat N_some-name (term Zero))
;; #true
(redex-match? nat (Plus1 N_a) (term Zero))
;; #false
(redex-match? nat (Plus1 N_0) (term (Plus1 (Plus1 Zero))))
;; #true
```

If `(redex-match? L p t)` is `#true`, then `(redex-match L p t)` shows how
 the term and pattern match, with respect to the named non-terminals.
In other words, each named non-terminal binds a subterm.

```
(redex-match nat N_0 (term Zero))
;; (list (match (list (bind 'N_0 'Zero))))
(redex-match nat (Plus1 N_0) (term Zero))
;; #f
(redex-match nat (Plus1 N_0) (term (Plus1 (Plus1 Zero))))
;; (list (match (list (bind 'N_0 '(Plus1 Zero)))))
```


### What is a named non-terminal?

A named non-terminal in a language `L` is an identifier of the form `X_Y`, where:

- `X` is a non-terminal from `L`
- `Y` is any identifier

The name helps when one pattern contains multiple occurrences of the same non-terminal.

```
(define-language trees
  [binary-tree ::= Leaf
                   (Node binary-tree binary-tree)])

(redex-match trees
  (Node binary-tree_left binary-tree_right)
  (term (Node Leaf (Node Leaf Leaf))))
;; (list
;;  (match
;;   (list (bind 'binary-tree_left 'Leaf)
;;         (bind 'binary-tree_right '(Node Leaf Leaf)))))
```


### What else can patterns express?

Redex patterns may contain special identifiers to guide pattern-matching.
See the Redex reference for the full pattern language.

- the `_` pattern matches any term:

```
(redex-match? nat _ (term Zero))
;; #true
(redex-match? nat _ (term (A (B C))))
;; #true
(redex-match? nat (Plus1 _) (term (Plus1 9)))
;; #true
```

A `_` pattern does not bind.

```
(redex-match trees
  (Node _ _)
  (term (Node Leaf (Node Leaf Leaf))))
;; (list (match '()))
```

- the `...` pattern matches any sequence of the preceding pattern

```
(redex-match? nat (N_0 ...) (term ()))
;; #true
(redex-match? nat (N_0 ...) (term (Zero)))
;; #true
(redex-match? nat (N_0 ...) (term (Zero Zero Zero)))
;; #true
```

If a named non-terminal appears in the pattern before a `...`, then the
 non-terminal binds a sequence of subterms.

```
(redex-match nat (N_0 ...) (term (Zero Zero Zero)))
;; (list (match (list (bind 'N_0 '(Zero Zero Zero)))))
(redex-match nat (N_0 N_1 ... N_2) (term (Zero Zero)))
;; (list (match (list (bind 'N_0 'Zero) (bind 'N_1 '()) (bind 'N_2 'Zero))))
```

A `...` by itself is an error.

```
(redex-match? nat ... (term Zero))
;; redex-match?: found an ellipsis outside of a sequence
(redex-match? nat (...) (term Zero))
;; redex-match?: ellipsis should not appear in the first position of a sequence
```


### What can patterns **not** express?

- Disjunctions of patterns, e.g., "number or boolean".
  Use a language non-terminal for this.


### What is a judgment?

A Redex _judgment form_ defines a relation on terms.
The relation is defined by a set of inference rules.

Programming languages papers love to use inference rules.
Redex can express many of the judgments in papers; for example:

- well-formedness conditions (i.e., whether a term contains free variables)
- type checking rules
- type inference rules
- evaluation relations

Every judgment needs (1) a language (2) a mode (3) a contract (4) a set of inference rules.
For example, the following judgment defines an equality relation on natural numbers.

```
(define-language nat
  [N ::= Zero (Plus1 N)])

(define-judgment-form nat
  #:mode (N= I I)
  #:contract (N= N N)
  [
   --- Zero=
   (N= Zero Zero)]
  [
   (where (Plus1 N_0--) N_0)
   (where (Plus1 N_1--) N_1)
   (N= N_0-- N_1--)
   --- Plus1=
   (N= N_0 N_1)])
```

1. the language is `nat`; Redex uses the language to interpret patterns
2. the mode is `(N= I I)`; this means `N=` is the name of a judgment that
   expects two input terms (or, `N=` is a binary relation on terms)
3. the contract is `(N= N N)`; in other words, `N=` expects two terms that
   match the `N` non-terminal from the `nat` language
4. there are two inference rules, named `Zero=` and `Plus1=`
   - the `Zero=` rule states that `(N= Zero Zero)` always holds
   - the `Plus1=` rule states that `(N= N_0 N_1)` holds if `N_0` and `N_1`
     are both `Plus1` terms whose contents are related by `N=`

```
(judgment-holds (N= Zero Zero))
;; #true
(judgment-holds (N= (Plus1 (Plus1 Zero)) (Plus1 (Plus1 Zero))))
;; #false
(judgment-holds (N= (Plus1 Zero) (Plus1 (Plus1 Zero))))
;; #true
```

**Note** the inference rules form a _set_, not a _sequence_.
So when you ask Redex whether `(judgment-holds (N= Zero Zero))`, it applies
 all rules that match `(N= Zero Zero)`.
(In our case this is just one rule, but in general it could be many rules.)


### What is  `#:mode`?

A `#:mode` declaration expects a list of the form `(id pos-use ...)`,
 where `id` is an identifier and each `pos-use` is either
 `I` or `O`.
These declarations say four things:

1. `id` is the name of a new judgment form
2. `id` expects `N` arguments, where `N` is the number of `pos-use` symbols
3. `id` expects an _input_ at each position where the mode contains an `I`
4. `id` produces an _output_ at each position where the mode contains an `O`

As an example of outputs, here's a fast an easy type inference judgment
 for arithmetic expressions.
Given any term `e_0`, the judgment outputs the type `Int`.

```
(define-language Arith
  (e ::= integer (e + e))
  (τ ::= Int))

(define-judgment-form Arith
  #:mode (infer-type I O)
  #:contract (infer-type e τ)
  [
   --- T-Int
   (infer-type e_0 Int)])
```


### What can judgments **not** express?

Redex does not support inference rules that require guessing.

One example of this is a transitivity rule: "`τ_0` is related to `τ_2`
 if there exists a `τ_1` such that `τ_0` is related to `τ_1` and `τ_1` is
 related to `τ_2`".
The following example tries to define a transitive subtyping (`<:`) relation,
 but Redex rejects the `S-Trans` rule.

```
(define-language SomeTypes
  (τ ::= (→ τ τ) Integer))

(define-judgment-form SomeTypes
  #:mode (<: I I)
  #:contract (<: τ τ)
  [
   (<: τ_0 τ_1)
   (<: τ_1 τ_2)
   --- S-Trans
   (<: τ_0 τ_2)]
  [
   --- S-Refl
   (<: τ_0 τ_0)]
  [
   (<: τ_dom-1 τ_dom-0)
   (<: τ_cod-0 τ_cod-1)
   --- S-Arrow
   (<: (→ τ_dom-0 τ_cod-0) (→ τ_dom-1 τ_cod-1))])
```

Specifically, the error is that the named non-terminal `τ_1` appears in an
 input position but is not bound to a term.


### What is a metafunction?

A metafunction is a term-level function on terms.

Every metafunction needs: (1) a language (2) a name (3) a contract (4) a sequence of guarded input/output cases.

Here is a metafunction that returns `#true` when given two equal natural numbers.
The definition is similar to the `N=` judgment form.

```
(define-metafunction nat
  N=? : N N -> boolean
  [(N=? Zero Zero)
   #true]
  [(N=? N_0 N_1)
   (N=? N_0-- N_1--)
   (where (Plus1 N_0--) N_0)
   (where (Plus1 N_1--) N_1)]
  [(N=? N_0 N_1)
   #false])
```

- the metafunction is named `N=?`
- its contract is `N N -> boolean`, this means `N=?` expects 2 terms that
  match the `N` pattern and returns a term that matches the pattern `boolean`
- there are three cases; the second case is guarded by two `where` clauses

Any occurrence of `(N=? ....)` in a term is evaluated.

```
(term (N=? (Plus1 (Plus1 Zero)) (Plus1 (Plus1 Zero))))
;; #true
(term ((N=? Zero Zero) Zero))
;; '(#true Zero)
(term (N=? (Plus1 Zero) (Plus1 (Plus1 Zero))))
;; #false
```

Any occurrence of `N=?` outside a `term` is an error.


**Note** the cases in a metafunction form a _sequence_, not a _set_.
To evaluate a metafunction application, Redex tries each case in order
 and returns the result of the first case that (1) matches the call-site (2)
 for which all guards succeed.


### Should I use a metafunction or a judgment form?

Use a judgment form.

Metafunctions are handy, but judgments are easier to read and debug and maintain.


### What is a reduction relation?

TODO


### What is "Racket mode"? What is "Redex mode"?

TODO


### Are `side-condition`s evaluated in "Racket mode" or "Redex mode"?

A `(side-condition e)` sometimes evaluates `e` as a Racket expression
 and sometimes evaluates `e` as a Redex expression.

- reduction relations and metafunctions expect a **Racket** expression
- judgments expect a **Redex** expression


### What is a binding form?

In the lambda calculus, `λ`-terms bind variables.
A term `(λ x M)` means that any free occurrence of `x` in the sub-term `M`
 refers to the `x` from the `λ`-term.

Redex can express this idea with a binding form.

```
(define-language Λ
  [e ::= (e e) x (λ x e)]
  [x ::= variable-not-otherwise-mentioned]
  #:binding-forms
  (λ x_0 e_0 #:refers-to x_0))
```

Binding forms work together with Redex's functions for substitution and
 alphabetic equivalence.

```
(alpha-equivalent? Λ
  (term (λ x x))
  (term (λ y y))))
;; #true

(define-metafunction Λ
  test-substitute : e -> e
  [(test-substitute (λ x_0 e_0))
   (substitute e_0 x_0 y)])
(term (test-substitute (λ z (z z))))
;; '(y y)
```


### Where to learn more about Redex?

"Critical path" resources:

- Redex documentation: <http://docs.racket-lang.org/redex/index.html>
- Source code: <https://github.com/racket/redex>
- Tutorial: <https://dvanhorn.github.io/redex-aam-tutorial/>
- Source code for this post: TODO


"Procrastination" resources:

- Tree Automata: <http://tata.gforge.inria.fr/>
- _Should your Specification Language be Typed?_: <http://lamport.azurewebsites.net/pubs/lamport-types.pdf>
- Contribute to this FAQ: TODO

<!-- https://github.com/nuprl/website/tree/master/blog/_src/posts -->
